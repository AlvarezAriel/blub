// Applies preconditioner to the residual, yielding the auxilary vector usually denoted as z.

#version 460

#include "pressure.glsl"

// layout(set = 2, binding = 0) buffer restrict PcgScalars_ { PcgScalars Scalars; };
layout(set = 2, binding = 1) uniform texture3D Residual;
// layout(set = 2, binding = 2) uniform texture3D Read1;
layout(set = 2, binding = 3, r32f) uniform restrict writeonly image3D Auxilary;

COMPUTE_PASS_VOLUME

// TODO: This is a diagonal jacobi preconditioner. Easy to start off, but want to try Incomplete Poisson preconditioner instead!
// see https://software.intel.com/content/www/us/en/develop/articles/parallelized-incomplete-poisson-preconditioner-in-cloth-simulation.html
// and also https://github.com/tunabrain/gpu-fluid
// as well as https://github.com/austinEng/WebGL-PIC-FLIP-Fluid#pressure-solve
void main() {
    ivec3 gridCoord = ivec3(gl_GlobalInvocationID);
    float result = texelFetch(Residual, gridCoord, 0).x; // todo: early out if there's nothing to do here

    uint markerX0 = texelFetch(MarkerVolume, gridCoord - ivec3(1, 0, 0), 0).x;
    uint markerX1 = texelFetch(MarkerVolume, gridCoord + ivec3(1, 0, 0), 0).x;
    uint markerY0 = texelFetch(MarkerVolume, gridCoord - ivec3(0, 1, 0), 0).x;
    uint markerY1 = texelFetch(MarkerVolume, gridCoord + ivec3(0, 1, 0), 0).x;
    uint markerZ0 = texelFetch(MarkerVolume, gridCoord - ivec3(0, 0, 1), 0).x;
    uint markerZ1 = texelFetch(MarkerVolume, gridCoord + ivec3(0, 0, 1), 0).x;

    // This is the diagonal value of matrix A!
    float numNonSolidNeighbors = 0.0;
    numNonSolidNeighbors += float(markerX0 != CELL_SOLID);
    numNonSolidNeighbors += float(markerX1 != CELL_SOLID);
    numNonSolidNeighbors += float(markerY0 != CELL_SOLID);
    numNonSolidNeighbors += float(markerY1 != CELL_SOLID);
    numNonSolidNeighbors += float(markerZ0 != CELL_SOLID);
    numNonSolidNeighbors += float(markerZ1 != CELL_SOLID);
    if (numNonSolidNeighbors > 0.0)
        result /= numNonSolidNeighbors;

    imageStore(Auxilary, gridCoord, result.xxxx);
}
