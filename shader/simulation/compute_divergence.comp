// Computes velocity divergence of VelocityVolumeRead

#version 450

#include "bindings_write_particles_volume.glsl"
#include "utilities.glsl"

COMPUTE_PASS_VOLUME

const ivec3 dirs[3] = ivec3[3](ivec3(1, 0, 0), ivec3(0, 1, 0), ivec3(0, 0, 1));

void AddDivergenceInDir(inout float divergence, ivec3 gridCoord, uint dir) {
    // Divergence exists only from solid/fluid or fluid/fluid!
    vec4 vneg = texelFetch(VelocityVolumeRead, gridCoord - dirs[dir], 0);
    if (vneg.w != CELL_AIR) {
        vec4 vpos = texelFetch(VelocityVolumeRead, gridCoord + dirs[dir], 0);
        [[flatten]] if (vpos.w != CELL_AIR) divergence += vpos[dir] - vneg[dir];
    }
}

void main() {
    ivec3 gridCoord = ivec3(gl_GlobalInvocationID);
    float divergence = 0.0;

    // Don't compute divergence if there's no fluid
    // (Divergence tells us how much fluid is going in/out. If there's air/solid, then that's clearly zero!)
    vec4 mid = texelFetch(VelocityVolumeRead, gridCoord, 0);
    [[dont_flatten]] if (mid.w == CELL_FLUID) {
        AddDivergenceInDir(divergence, gridCoord, 0);
        AddDivergenceInDir(divergence, gridCoord, 1);
        AddDivergenceInDir(divergence, gridCoord, 2);
        divergence *= 0.5;
    }

    imageStore(DivergenceVolume, gridCoord, divergence.xxxx);
}
